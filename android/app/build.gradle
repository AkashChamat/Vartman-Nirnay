apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"

def enableHermes = true
def jscFlavor = 'org.webkit:android-jsc:+'

// Make hermesEnabled accessible to other modules
project.ext.react = [
    enableHermes: enableHermes,
    hermesEnabled: enableHermes,
    entryFile: "index.js",
    bundleInRelease: true
]

def enableProguardInReleaseBuilds = true

android {
    namespace "com.vartmannew"
    compileSdk rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.vartmannew"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 21
        versionName "1.1.1"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86_64"
        }

        // Default manifest placeholder
        manifestPlaceholders = [
            removeAdIdPermission: "keep"
        ]
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file(MYAPP_UPLOAD_STORE_FILE)
            storePassword MYAPP_UPLOAD_STORE_PASSWORD
            keyAlias MYAPP_UPLOAD_KEY_ALIAS
            keyPassword MYAPP_UPLOAD_KEY_PASSWORD
        }
        internal {
            initWith(release) // internal builds must be signed with release key for Play Store
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled enableProguardInReleaseBuilds
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
        }
        internal {
            initWith(release)
            signingConfig signingConfigs.internal
            // Remove AD_ID permission for internal builds
            manifestPlaceholders = [
                removeAdIdPermission: "remove"
            ]
            matchingFallbacks = ['release'] // fix variant mismatch
        }
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
            pickFirsts += ['**/*.so']
        }
        resources {
            excludes += [
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/NOTICE',
                'META-INF/NOTICE.txt'
            ]
        }
        pickFirst '**/libc++_shared.so'
        pickFirst '**/libjsc.so'
        pickFirst '**/libhermes.so'
    }

    bundle {
        language { enableSplit = false }
        density { enableSplit = false }
        abi { enableSplit = true }
    }
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:32.7.0')
    implementation 'com.google.firebase:firebase-messaging'

    // Firebase Analytics is included only for non-internal builds
    if (!project.gradle.startParameter.taskNames.any { it.toLowerCase().contains("internal") }) {
        implementation 'com.google.firebase:firebase-analytics'
    }

    implementation("com.facebook.react:react-android")
    implementation("com.facebook.react:flipper-integration")
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.activity:activity-ktx:1.8.2"

    if (enableHermes.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}

// React Native modules
apply from: file("../../node_modules/@react-native-community/cli-platform-android/native_modules.gradle")
applyNativeModulesAppBuildGradle(project)
apply from: "../../node_modules/react-native-vector-icons/fonts.gradle"

// Firebase plugin
apply plugin: 'com.google.gms.google-services'
